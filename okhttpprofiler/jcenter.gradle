
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.4'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/okhttpprofiler-release.aar")
            groupId "com.itkacher.okhttpprofiler"
            artifactId "okhttpprofiler"
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintrayUser')
    key = properties.getProperty('bintrayApiKey')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = "okhttpprofiler"
        name = "com.itkacher.okhttpprofiler"
        description = libraryDescription
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        dryRun = true
        version {
            name = this.version
            desc = libraryDescription
            released = new Date()
            vcsTag = this.version
        }
    }
}